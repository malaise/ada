# Syntax of the descriptor file
# -----------------------------
# <descriptor_file> ::= { <descriptor_def> }
#
# <descriptor_def> ::= DESCRIPTOR <descriptor_no>
#                        [ <list_def>  ]
#                        [ { <field_def> } ]
#                      END DESCRIPTOR
#
# <list_def>  ::= LIST
#                   <field_geo_and_color>
#                 END LIST
#
# <field_def> ::= FIELD <field_no> <field_type>
#                   <field_geo_and_color>
#                   [ <init_def> ]
#                 END FIELD
#
# <field_type> ::= PUT  |  BUTTON  |  GET
#
# <field_geo_and_color> ::= GEOMETRY <geometry>
#                           COLORS   <colors>
#
# <geometry> ::= <upper_left_row> <upper_left_col>
#                <lower_right_row> <lower_right_col>
#
# <colors> ::= <foreground_color> [ <blink> ]
#                          <background_color> [ <selected_color> ]
#
# <init_def> ::= INIT
#                  [ { <init_line> } ]
#                END INIT
#
# <init_line> ::= <init_row> <init_col> [ <init_string> ]
#
#
# Semantic of the descriptor file
# -------------------------------
# - <descriptor_no> has to be positive range 1 .. 50 and sole (no double)
# - LIST has to be defined none or once per descriptor
# - <field_no> have to be within FIELD_RANGE (1 .. 200), strictly crescent
#                    within one descriptor, no "hole", no double
# - <geometry> must define at least one row and one col
# - <geometry> must not make the field overlap with another field of the
#                    descriptor
# - <geometry> of a GET field must define only one row
# - <foreground_color> must be of COLORS for PUT fields
#                    and of BASIC_COLORS for BUTTON, GET and LIST
# - <background_color> and <selected_color> must be BASIC_COLORS
# - <blink> is mandatory for PUT fields and must not be specified
#                    for LIST, BUTTON and GET fields
# - <selected_color> is mandatory for LIST and GET fields and
#                    must not be specified for PUT and BUTTON
# - <init_row> have to be compatible with <geometry>
#                    crescent, no double (holes allowed)
# - <init_col> have to be compatible with <geometry>
# - <init_string> have to be compatible with <geometry> and <init_col>
#
# Infos
# -----
# Separator is space or tabulation
# Comments are lines with '#' as first signficant character
# Rows are from 0 to 24
# Cols are from 0 to 79
# Basic colors are BLACK, BLUE, GREEN, CYAN, RED, MAGENTA, BROWN, LIGHT_GRAY
# Colors are basic colors plus DARK_GRAY, LIGHT_BLUE, LIGHT_GREEN, ORANGE,
#                              YELLOW, WHITE
# Blink is "BLINK" or "NOT_BLINK"
# Upper case is mandatory for keywords, not significant for colors and blink,
#  significant for init_string
#
# Summary of color combinations
# -----------------------------
#  Type  |   Foreground  Blink  Background  Selected
#  -------------------------------------------------
#  PUT   |   X Colors    X      X Basic
#  BUTTON|   X Basic            X Basic
#  GET   |   X Basic            X Basic     X Basic
#  LIST  |   X Basic            X Basic     X Basic

