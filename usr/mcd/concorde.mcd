# Fuel consumption of Concorde
# Error message
[ "ERROR. Invalid argument" putl
"Usage: concorde <total_distance><unit> [ <subsonic_distance> ]" putl
"  <unit> ::= km | Nm | Mi" putl
1 setexit ssize popn retall ] E popr

# Check arguments
ssize dup dup ispos ~ E pushr ifcall 2 > E pushr ifcall
1 = "" ifthen s popr a popr s pushr strnull [ "0" s popr ] ifcall
a pushr dup dup strlen l copyr 3 < E pushr ifcall
# Split first arg
1 l pushr 2 - strsub t popr
l pushr 1 - l pushr strsub u popr

# Check unit
0.0 f popr u pushr dup dup
"km" = [ 1.000 f popr ] ifcall
"Nm" = [ 1.852 f popr ] ifcall
"Mi" = [ 1.609 f popr ] ifcall
f pushr 0.0 = E pushr ifcall

# Check distances and convert into km
[ dup pushr dup caninte  ~ E pushr ifcall strinte toreal f pushr * swap popr 
] C popr
s C pushr call
t C pushr call

# Compute total - (subsonic + transitions)  (transition=climb+descent, 450 NM)
3 format t pushr
450 v copyr toreal 1.852 * s pushr + - dup isneg
[ "ERROR. Subsonic plus transitions (" put v pushr put " Nm) is longer than total"
putl 1 setexit ssize popn retall ] ifcall
# Per 1000km; 11 T cruise and 26 T subsonic
11.0 * s pushr 26.0 * + 1000.0 /
# Add conso for Transistion + Ground + Security
38.0 +
# Put result
-1 roundat 5 3 " " normal 1 1 strdel put "T" putl

