# Francs <-> Euros conversion

# Error and quit
[ [ Error, 1 argument: <amount><unit> expected ] ;
  [ "   amount : float or integer" ] ;
  [ "   unit   :  f or e" ] ;
0 retn ] E popr

# Francs in an euro
6.55957 e popr

# Check 1 arg
ssize 1 /= E pushr ifcall
# Store arg
a copyr

# Parse arg
strlen l copyr
# At least two chars
2 < E pushr ifcall

# Extract unit - last char
a pushr l pushr dup strsub u copyr
# Unit is f or e
[ f ] /= u pushr [ e ] /= & E pushr ifcall

# Extract amount value
a pushr 1 l pushr 1 - strsub v copyr
# locate '.'
1 [ . ] v pushr strloc
# Convert to real
0 = [ strinte toreal ] [ strreal ] ifte call
v popr


# Set conversion factor and result unit
6.55957 u pushr [ f ] = [ 1.0 swap / [ e ] r popr ] [ [ f ] r popr ] ifte call

# Convert
v pushr *

# Round cents and display "int."
100.0 *
tointe dup
9 format 100 / put [ . ] put
# Display frac (2 digits) and unit
100 % dup 10 < [ [ 0 ] put 1 format put ] [ 2 format put ] ifte call
r pushr putl

