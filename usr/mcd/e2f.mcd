# Francs <-> Euros conversion

# Error and quit
[ [ Error, 1 argument: <amount><unit> expected ] ;
  [ "   amount : float or integer" ] ;
  [ "   unit   :  f or e" ] ;
0 retn ] E popr

# Francs in an euro
6.55957 e popr

# Check 1 arg
ssize 1 /= E pushr ifcall
# Store arg
a copyr

# Parse arg
strlen l copyr
# At least two chars
2 < E pushr ifcall

# Extract unit - last char
a pushr l pushr dup strsub u popr
# Unit is f or e
u pushr [ f ] /= u pushr [ e ] /= & E pushr ifcall

# Extract amount value
a pushr 1 l pushr 1 - strsub v copyr
# locate '.'
1 [ . ] v pushr strloc
# Convert to real
0 = [ strinte toreal ] [ strreal ] ifte call
v popr


# Set conversion factor and result unit
[ f ] r popr
6.55957 u pushr [ f ] = [ 1.0 swap / [ e ] r popr ] ifcall

# Convert
v pushr *

# Display int.frac
100.0 *
tointe dup
9 format 100 / put [ . ] put 2 format 100 % put r pushr putl

