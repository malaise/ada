# Display Chrono at format DdTHh:Mm:Ss.Mil each second
# T0
clock t popr

# Function: Display current chrono value
# If ToS is true then remove millisecs
[ clock t pushr -
swap
[ toreal 3 roundat round daysof 1 14 strsub ]
[ daysof ] ifte call
putl ] P popr

# Function on input key or break: Display chrono and exit
[ false P pushr call isstdio ~ [ true inecho ] ifcall retall ] B popr
[ newl B pushr call ] callbrk

# No arg expected
ssize isntnull [ "ERROR. No argument expected" putl 1 setexit retall ]
ifcall

# Configure terminal and dedicated help message
isstdio [ "Break (Ctrl C) to stop" ] [ false inecho "Any key to stop" ] ifte
call putl

# Loop
0 i popr
[
# Compute next appointment
t pushr i pushr 1000 * + clock - toreal 1000.0 /
# Wait until it
[ sleep ] [ inkeytim isbool ~ [ B pushr call ] ifcall ]
isstdio etfi call
# Display chrono with precision second
true P pushr call
# Next loop
i pushr 1 + i popr
# Loop
pushle copye retacal
] copye
call

