# Display Clock at format Mm/Dd-Hh:Mm:Ss.Mil
# With no arg:  display current time then at each round minute
# With arg 's': display current time then at each round second

# Arg error
[ "Usage: Clock [ s ]" putl ssize popn 1 setexit retall ] E popr

# 1 optionnal arg
60_000 s popr
ssize 1 = [
  dup isreg ~ E pushr ifcall
  s /= E pushr ifcall
  1_000 s popr
] ifcall

# 0 arg remaining
ssize isnotnul E pushr ifcall

# Time string
[ dateof 6 23 strsub ] S popr

# Initial display
clock dup S pushr call putl

# Simulate prev appointment (trunc to XXmn00s.000 or XXmnmnYYs.000)
s pushr / s pushr * a popr

# Loop
[
# Compute next appointment
a pushr s pushr + a copyr
# Compute delay and sleep
clock - toreal 1000.0 / sleep
# Display time
clock S pushr call putl
# Loop
pushle copye retacal
] copye
call

